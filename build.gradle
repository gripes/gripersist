import org.gradle.plugins.ide.eclipse.model.SourceFolder

//dependsOn(':gripes-ast')

apply {
	plugin 'groovy'
	plugin 'eclipse'
}

logger.quiet "Building Gripersist"

group = 'gripes'
archivesBaseName = 'gripersist'
version = '0.1.6'

versions = [
	'gripes-web' : '0.1.8',
	'gripes-plugin' : '0.1.9',
	'gripersist' : '0.1.5',
	'gripes-ast' : '0.1.0',
	'hibernate-core' : '4.0.1.Final',
	'tomcat':'6.0.35'
]

repositories {
    mavenCentral()
    mavenRepo url: "http://www.gripes-project.org/libs"
    mavenRepo name: "jboss-snapshots", url: "http://snapshots.jboss.org/maven2/"
}

configurations {
	ftpAntTask {}
	provided {}
}

sourceSets {
	main {
		compileClasspath += configurations.provided

		groovy { srcDir 'src' }
	    resources { srcDir 'resources' }
	}
	test {
		groovy { srcDir 'test' }
		resources { srcDir 'test-resources' }
	}
}

dependencies {
	//** Ant FTP
	ftpAntTask module("org.apache.ant:ant-commons-net:1.7.0") {
	    module("commons-net:commons-net:1.4.1") {
	        dependencies("oro:oro:2.0.8@jar")
	    }
	}
	
	//** Groovy
	groovy (group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.4')
	compile (group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.4')
	
	//** Servlet
//	testCompile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
	testCompile group: 'javax.servlet', name: 'jsp-api', version: '2.0'

	//** Framework
	compile group: 'net.sourceforge.stripes', name: 'stripes', version: '1.5.6'
	
	//** Database
	testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.0.0'
	compile group: 'org.hibernate', name: 'hibernate-core', version: versions['hibernate-core']
	compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: versions['hibernate-core']
	runtime group: 'org.hibernate', name: 'hibernate-c3p0', version: versions['hibernate-core']
	
	//** Logging
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
	compile group: 'ch.qos.logback', name: 'logback-core', version: '0.9.28'
	compile group: 'ch.qos.logback', name: 'logback-access', version: '0.9.28'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '0.9.28'
	
	//** Testing
	testCompile 'junit:junit:4.10'
	
	//** Tomcat
//	testCompile	"org.apache.tomcat:catalina:${versions['tomcat']}",
//				"org.apache.tomcat:coyote:${versions['tomcat']}",
//				"org.apache.tomcat:jasper:${versions['tomcat']}"

	
	compile 'org.apache.tomcat:catalina:6.0.35'
	compile 'org.apache.tomcat:coyote:6.0.35'
	compile 'org.apache.tomcat:jasper:6.0.35'
	compile 'org.apache.tomcat:jsp-api:6.0.35'
	compile group: 'org.apache.tomcat', name: 'servlet-api', version: '6.0.35'
	compile group: 'org.apache.tomcat', name: 'jsp-api', version: '6.0.35'
					
//	compile(project(":gripes-ast")){changing=true}
	
	compile("gripes:gripes-ast:0.1.0")
}

eclipse {
	classpath {
		defaultOutputDir=file("bin/main")
		file {
			withXml {
				def xml = it.asNode()
				xml."classpathentry".findAll{it."@kind"=="src" && it."@path".find("test")}*."@output"="bin/test"
			}
			whenMerged { classpath ->
				classpath.entries.removeAll(classpath.entries.findAll{((it instanceof SourceFolder) && it.dir==null)})
//				classpath.entries.removeAll(classpath.entries.find{it.path.find("servlet-api-2.4")})
			}
		}
	}
}
task eclipseCp(dependsOn: eclipseClasspath) {}

gradle.taskGraph.whenReady {taskGraph ->
	if (taskGraph.hasTask(':release')) {
		version = "1.0"
	} else {
		version = "${version}"//-SNAPSHOT-"+new GregorianCalendar().timeInMillis
	}
	logger.quiet "Version set to $version"
}

uploadArchives {
    repositories {
       flatDir(dirs: file('repos'))
    }
}
uploadArchives.doLast {    
	ant {
		taskdef(name: 'ftp',
                classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
                classpath: configurations.ftpAntTask.asPath)

		ftp(action: "mkdir",
			server: "www.gripes-project.org", 
			remotedir: "${ftpUploadRoot}/libs/gripes/gripersist/${version}",
			chmod: "0755",
			userid: ftpUploadUser,
			password: ftpUploadPassword)
			
		ftp(action: "put",
			server: "www.gripes-project.org", 
			remotedir: "${ftpUploadRoot}/libs/gripes/gripersist/${version}",
			chmod: "0755",
			userid: ftpUploadUser,
			password: ftpUploadPassword) {
			fileset(dir:"repos", includes: "*.*")
		}
    }
}